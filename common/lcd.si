// @sylefeb 2022
// MIT license, see LICENSE_MIT in Silice repo root
// https://github.com/sylefeb/Silice

import('../common/ext/ice40_sb_io.v')
import('../common/ext/ice40_sb_io_ddr.v')

// wrapper around single bit sb_io primitive
unit sb_io_n(input auto o,output sameas(o) p)
{
  $$for i=0,widthof('o')-1 do
  sb_io _(clock <: clock, out <: o[$i$,1], pin :> p[$i$,1]);
  $$end
}

// For ILI9341, parallel screen interface
unit parallel_screen(
  input   uint1 start,    input  uint1 data_or_command, input  uint8 byte,
  output  uint8 screen_d, output uint1 screen_dc, output uint1 screen_wrn
) {
  uint1 scr_trigger(1);
  uint8 scr_byte(0);
  uint1 scr_dc(0);
  uint1 high(1);
  // sends data through DDR, so one byte can be sent every cycle (fast!)
  sb_io_ddr _(clock <: clock, out_0 <: scr_trigger, out_1 <: high, pin :> screen_wrn);
  sb_io_n   _(o                     <: scr_byte,    p   :> screen_d);
  sb_io     _(clock <: clock, out   <: scr_dc,      pin :> screen_dc);
  always {
    scr_byte    =  byte;
    scr_dc      =  data_or_command;
    scr_trigger = ~start;
  }
}

// Piece of circuitry repeated to send a byte to the screen
circuitry lcd_write(input byte, input cmd_else_dta)
{
  screen.byte            = byte;
  screen.data_or_command = ~cmd_else_dta;
  screen.start           = 1;
}

// Unit to drive the LCD
unit lcd_driver(
  output uint8  lcd_d,
  output uint1  lcd_rs,
  output uint1  lcd_wr_n,
  output uint1  lcd_cs_n(0),
  output uint1  lcd_rst_n(1),
  input  uint1  lcd_mode,
  input  uint1  lcd_fmark,
  output uint1  ready(0),
  input  uint1  valid(0),
  input  uint8  data,
) {

  // screen interface
  parallel_screen screen<reginputs>(
    screen_d :> lcd_d, screen_dc :> lcd_rs, screen_wrn :> lcd_wr_n
  );

  // init sequence for full screen (ILI9341)
  brom uint9 lcd_init[32] = {
    //v command or data
    9h101,
    9h111,
    9h13a,
    9h055,
    9h136,
    9h040,
    9h113,
    9h129,
    9h12b,
    9h000,
    9h000,
    9h001,
    9h03f,
    9h12a,
    9h000,
    9h000,
    9h000,
    9h0ef,
    9h151,
    9h000,
    9h12c,
    pad(0)
  };

  always_before {
    lcd_rst_n = 1; lcd_cs_n  = 0;
  }

  algorithm <autorun> {

    while (~lcd_mode) { }

    lcd_init.addr = 0;
    while (lcd_init.addr != 21) {
      () = lcd_write(lcd_init.rdata[0,8],
                     lcd_init.rdata[8,1]);
      lcd_init.addr = lcd_init.addr + 1;
    }

    while (1) {
      ready        = 1;
      screen.byte  = data;
      screen.data_or_command = 1;
      screen.start = valid;
    }
  }
}
